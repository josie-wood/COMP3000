title: IntroduceMouse
tags:
---
//first time introduction
You: *knock knock*
        ...
        Hm.
        No immediate answer.
        I thought they were home?
Mouse: zzzzzz
        Ah.
        Guess that explains it.
        ...
You: hello?
Mouse:  zzzzzz...
        Guess I've gotta pull out the big guns.
You:    *KNOCK KNOCK*
You:    HELLOOOOOO?
Mouse:  zzz-dsaasd? huh?
        Success!
Mouse:  wasgoinon....
You:    um, could you come to the door please?
Mouse:  Ah! 
Mouse:  Yes!
Mouse:  Right!
Mouse:  One second!
//sounds of slippers
//mouse appears
Mouse:  *yawwwwn* 
Mouse:  Good morn- afternoon?
Mouse:  Evening?
Mouse:  Ah whatever.
Mouse:  Hi!
You:    Hello!
        ...
        I didn't think this far ahead. 
        What do I say now?!

-> say something smart
-> say something funny        
        
You:    ...
        can't think can't think can't thi-
Mouse:  Uh, can I help you with anything?
        Dangit, words are hard.
        Just say something! Anything!
You:    Yes!
You:    I mean no?
        I woke them up from their nap for this?!
You:    I mean - I just moved in opposite!
You:    I just wanted to introduce myself?
You:    ... so hello!
        They smiled at that!
        Nice
        Well recovered
Mouse:  Haha, hello!
Mouse:  I'm sorry I haven't been round to yours earlier
Mouse:  Coulda brought you a casserole or something
        what's a casserole?
Mouse:  Isn't that what neighbours do?
Mouse:  But I haven't really been on top of things lately
Mouse:  Sorry...
You:    Oh no don't worry about it!
You:    I've just been settling anyway
You:    It's been a few weeks and I'm only now getting the courage to talk to people! aha
        They didn't need to know that.
        Hey ho.
Mouse:  I get it
Mouse:  I can never gather the oomph to get out and about anymore
Mouse:  Let alone start introducing myself to strangers!
You:    Being a person is hard.
Mouse:  Agreed.
        Okay, now to find some small talk, I guess?
        That's what neighbours do?
        That and something to do with casserole, apparently, but I'll burn that bridge when I get to it.

<<set $smallTalk to 0>>
<<advanceEntryNode talkAgain>>

<<jump discussion>>

===

title: talkAgain
---
//brief nicities to replace introduction when you speak again before diving into discussion

Mouse:  Hi again!
You:    Hi again!

<<if !$smallTalk1Done>>
        //smalltalk 1
        You:    Nice weather we're having, eh?
                Classic neighbour talk
        Mouse:  Oh yeah! Hadn't noticed...
        Mouse:  It's not too shabby.
<<elseif !$smallTalk2Done>>
        You:    So... 
        You:    What's the deal with airplane food?
        You:    amiright?
        Mouse:  It is pretty crappy I guess?
        Mouse:  Is that all you came here to say this time?
<<elseif !$smallTalk3Done>>
        You:    I like your pyjamas, by the way
        Mouse:  Me too!
        Mouse:  They have pockets!
        You:    Ooh niiiiice
<<else>>
        You:    Let's cut to the chase, shall we?
        Mouse:  Sounds good.
        Mouse:  Whatcha wanna talk about?
<<endif>>

//reset values based on option just played
<<if !$smallTalk1Done>>
        //smalltalk 1 set as done
        <<set $smallTalk1Done to true>>
<<elseif !$smallTalk2Done>>
        //set smalltalk2 as done
        <<set $smallTalk2Done to true>>
<<elseif !$smallTalk3Done>>
        //set smalltalk3 as done
        <<set $smallTalk3Done to true>>
<<endif>>

<<jump discussion>>
===

title: discussion
tags:
---

//figure out if first two choices are done
<<if $lookedAtThem>>
        <<if $lookedAtRoom>>
                <<if !$Learnt_Depression>>
                        <<set $ShowStoppingYouOption to true>>
                <<endif>>
        <<endif>>
<<endif>>

//figure out if learnt depression and haven't asked what makes happy
<<if !$askedWhatHappy>>
        <<if $Learnt_Depression>>
                <<set $ShowAskHappyOption to true>>
        <<endif>>
<<endif>>
// first level of choices 

-> Look at them  <<if !$lookedAtThem>>
    <<jump lookAtThem>>
->Look around the room <<if !$lookedAtRoom>>
    <<jump lookAtRoom>>

// if looked at them and room but haven't learnt depression
//aka chose comebackagain so learnt forage
//and chose askphotos so learnt hiking

-> 'What's been stopping you from foraging and exploring?' <<if $ShowStoppingYouOption>>
        <<jump askStoppingYou>>
//if LearnDepression
-> 'What do you like to do?' <<if $ShowAskHappyOption>> 
    <<jump askWhatMakesHappy>>

//if LearnHiking
-> 'Would you like to go for a walk?' <<if $askedWhatHappy>>
        <<jump askToWalk>>

-> Walk away
        You:    Okay, imma head out now
        You:    Byeeee!
        Mouse:  Bye!
        <<stop>>
===

title: lookAtThem
---

<<set $lookedAtThem to true>>
//description of them, body language, vibes - sleepy, unmotivated, stuck in a rut
    They're wearing a pair of cosy pyjamas
    maybe they're having a cosy day at home?
    And a midday nap?
    But from the bags under their eyes 
    and the way they squint into the sunlight
    it doesn't look like they've had a restful nights sleep in a while.
    They're still smiling at me
    but their eyes look weary, and blurry,
    and keep darting back inside.
    They're still smiling
    but the somehow still look...
    sad. 

->Come back another time?
    <<jump comeBackAgain>>
->Ask how they're doing
        <<jump askHowDoing>>

===

title: comeBackAgain
---
You:    I'm sorry I woke you, 
You:    Maybe I can swing by another day to chat more?
You:    You probably want to start making dinner or something anyway.
        In hindisght, I probably should've left when I heard snoring. 
        Oops.
        Hope they aren't too put out.
Mouse:  Oh? Okay...
        Their smile dropped a bit
        Now I feel bad!
        I woke them up and then tried to leave straight away, that's even worse!
Mouse:  I suppose I could do with eating something...
Mouse:  It's been a while,
Mouse:  I just don't have it in me to cook these days
Mouse:  Looks like soup for the tenth time this week
You:    Oh wow.
You:    That's a lot of soup.

-> Don't you get bored of that much soup?!
    Mouse:  Of course I do!
    Mouse:  It's far too much soup!
    You:    I mean, yeah!
    You:    That's what I was thinking!
    You:    So you aren't feeling a bazillionish bowl of soup, eh?
    Mouse:  You could say that. 
    Mouse:  I could really go for a nice roast
            Aren't mice vegetarian?
    Mouse:  A nut roast, of course
            Knew it.        
    Mouse:  Ooh or maybe a pie?
    Mouse:  A tart?
    Mouse:  I once had this amazing tart, 
    Mouse:  Cheesy and onion-y and full of wild garlic
    -> Wild Garlic?
        You:    Wild Garlic?
        You:    I've never had that before
        You:    Is it good?
        Mouse:  Oh it's FAB
        Mouse:  Like garlicky spinach?
        Mouse:  ... it's better than it sounds
        Mouse:  And everything tastes good when you gather the ingredients yourself, I suppose
    -> Make a cheesy pun
                Okay, lets do this
        You:    That sounds pretty GOUDA
        You:    ...amiright?
                Smooth
        Mouse:  heh
        Mouse:  nice
        Mouse:  I BRIE was you did there
        You:    Heh thankyou        
-> Soup is pretty great.
    Mouse:  Haha, it certainly can be
    Mouse:  I used to make the best soups
    Mouse:  Whatever I could find in the woods, thrown into a pot
    Mouse:  It was different every time,
    Mouse:  But somehow also great every time?
            Great, now I want soup. 
    You:    I'd love to try it sometime
            Mouse smiled at that, too
    Mouse:  Maybe one day. 
    Mouse:  It's just boring canned stuff today though, 
    Mouse:  Haven't gotten out to the woods for months
    Mouse:  So you're not missing anything 


//discuss dinner, cooking habits, once foraged, now boring, low effort, survival
<<set $Learnt_Forage to true>>
<<jump discussion>>

===

title: askHowDoing
---
//ask them how they're getting on
//learn about their depression
//set LEARNT info: depression

You:    I hope you don't mind me asking, 
You:    But are you okay?
Mouse:  Huh? Oh right
Mouse:  This is normal for me,
Mouse:  No need to worry 

-> 'That doesn't really answer my question'
        You:    You look kinda...
        You:    Sad.
        You:    So. Are you okay?
        Mouse:  I mean
        Mouse:  I guess I am kinda sad?
        Mouse:  But mainly I'm just...
        You:    Numb? 
        Mouse:  Exactly.
        Mouse:  So you get it?
        You:    Unfortunately so.
        Mouse:  Brains suck. 
        You:    Brains SUCK.
                
-> 'It doesn't sound like you're okay'
        You:    just like 'not okay' has become 'normal' for you
        Mouse:  ...Yeah I guess that's about right
        Mouse:  But there's really no need to worry!
        Mouse:  I'm fiiiiine
                Riiiiiiight
        You:    Maybe you could feel better than fiiiiiine though
        Mouse:  Maybe.
        Mouse:  It's just... 
        Mouse:  Hard.
        Mouse:  And I've gotten so used to it being like this.
        You:    That doesn't sound fun. 
        You:    Brains suck. 
        Mouse:  Brains SUCK.


<<if !$Learnt_Depression>>
        <<set $Learnt_Depression to true>>
<<endif>>

<<jump discussion>>
===

title: lookAtRoom
---
<<set $lookedAtRoom to true>>
//describe their house
        The room behind them is dark,
        It's going to take my eyes a minute to adjust.
        Ah, the curtain are shut, that tracks.
        So I guess their snoring was a full on sleep?
        Not a cosy spur-of-the-moment nap?
        This place is set up in full on sleep-mode. 
        ...
        My eyes are starting to adjust!
        ...And there's stuff EVERYWHERE in there!
        The walls are plastered in frames
        Filled with mountains and nature and smiling faces.
        Speaking of mountains-
        There's a massive lump of boots
        Towered up nearly as tall as mouse
        How do they even reach them?
        They're all covered in that dry, dusty mud,
        Which whips around as the breeze comes through the door.


-> Ask about photos and old boots
        <<jump askPhotos>>
-> Ask about curtains
        <<jump askCurtains>>
===

title: askPhotos
---
//discuss photos
//used to go out and about a lot, travelled, explored
//been stuck in, lost touch, trapped

You:    I like all those photos behind you!
You:    Did you see all of those places?
Mouse:  Oh those?
Mouse:  Yeah!
Mouse:  Pretty, right?
You:    So pretty!
Mouse:  I love getting out into nature, 
Mouse:  Exploring a new place
Mouse:  Climbing up a tricky trail, 
Mouse:  Almost giving up,
Mouse:  But then being rewarded by that view at the top!

-> 'Maybe we could explore together one day?'
        You:    I'd love to take advantage of all of the nature around us here
        You:    And it looks like I've found a pro-guide!
                C'mon flattery, work your magic!
        Mouse:  Well... I could maybe recommend some places?
        Mouse:  But it's been a long time. 
        Mouse:  I kinda lost touch with people I used to go with
        Mouse:  I wouldn't call myself a pro, I can barely get out of bed!
        You:    I thought you liked -
        You:    no, LOVED -
        You:    Getting out and exploring!
        Mouse:  I do!
        Mouse:  Or I did?
        Mouse:  One day I'm sure I'll get back into it.
        You:    I look forward to that.

-> 'When was the last time you went out?'
        Mouse:  It's beeeen...
        Mouse:  a while. 
                Well duh.
        You:    Yeah, the dusty boots kinda gave that away
        Mouse:  Yup. 
        Mouse:  Nature used to be my happy place
        Mouse:  Where I felt the most myself, ya'know?
        Mouse:  But now it all just feels...
        Mouse:  Out of reach.
        You:    I'm pretty sure I saw a little woodland down the end of the lane?
        You:    That's pretty close?
        Mouse:  Not that kind of out of reach.
        Mouse:  I'm... scared to go back out there again.
        Mouse:  Who would I go with?
        Mouse:  Am I still strong enough?
        Mouse:  And what if...
        Mouse:  ...it doesn't make me happy like it used to?
                ...
                oof
        You:    ... Those are some scary thoughts.
        You:    Maybe one day we could go together?
        You:    And not go too far?
        You:    And we'll have the best time we can?
        Mouse:  Awh, that's sweet.
        Mouse:  Maybe one day.

<<set $Learnt_Hiking to true>>
//set LEARNT info: hiking
<<jump discussion>>
===

title: askCurtains
---

You:    It looks like you've got a real cosy set up in there?
You:    With the curtains and everything
        Subtle
You:    Could definitely have a good ol' catnap in there
Mouse:  Cosy, eh?
Mouse:  Yeah, I guess it is. 
Mouse:  I've had it set up like this for ages now
Mouse:  With my sleep being so difficult, 
Mouse:  And with me feeling...weird
Mouse:  It just kinda made sense to shut the world out, 
Mouse:  Rest up, you know?
You:    I see. 
You:    Do you think it makes you feel better?
Mouse:  ...no.
Mouse:  But I think it makes me feel less bad about feeling bad?
Mouse:  Helps me hide away from it, a bit.
// more info about depression
//if not yet - then set LEARNT info: depression
//provide character insight
<<if !$Learnt_Depression>>
        <<set $Learnt_Depression to true>>
<<endif>>

<<jump discussion>>
===

title: askStoppingYou
---
//reveal depression if haven't found it from the other options
You:    It sounds like something really important to you
You:    What's making it hard?
Mouse:  It's just...
Mouse:  Easier said than done, at the moment
Mouse:  I love the IDEA of getting back out there again!
Mouse:  But in practice I can barely keep myself awake and fed
Mouse:  The outside world is intimidating
You:    Sometimes just existing is tricky
Mouse:  It is!
You:    Maybe we can work together and try and make it a bit easier?
Mouse:  ... I feel guilty asking that of you.
You:    Psssh nah don't feel bad
You:    It'll probably make me feel better too
You:    Win-win!
You:    Deal?
Mouse:  ...Maybe.
Mouse:  With your help.
Mouse:  Thankyou.

<<set $Learnt_Depression to true>>
<<set $ShowStoppingYouOption to false>>

<<jump discussion>>
===
title: askWhatMakesHappy
---
// option only available if know about depression
You:    SO, what makes you happiest?
<<if $Learnt_Forage>>
You:    Foraging for food in nature?
<<endif>>
<<if $Learnt_Hiking>>
You:    Exploring new places?
<<endif>>
Mouse:  Jeez, that's a big question
Mouse:  What makes you happiest, eh?

-> 'Nice deflection.'
        You:    You're avoiding the question!
        Mouse:  So are you!
        You:    Well you started it!
-> 'I'm trying to figure that out.'
        You:    New place, new me, new happy, ya'know?
        You:    I'm just trying to get out and meet people and try things
        You:    And hopefully find happy along the way
        Mouse:  ...So you wanna try and copy my happy?
        You:    Sure, yeah, that would be ideal

Mouse:  Ugh, fine. 
Mouse:  I guess I've always been happiest
Mouse:  When I'm out in nature
Mouse:  Connecting with it
Mouse:  It makes me feel small, 
Mouse:  But in a good way, if that makes sense?
You:    I think I get it?
You:    Maybe?
        Probably not. 

<<if $Learnt_Forage>>
        You:    Like the foraging stuff, 
        You:    Living off of nature and stuff like that?
        Mouse:  Yeah! 
                I do get it! Nice!
        Mouse:  I tend to get caught up in big-picture stuff
        Mouse:  So it's nice to go back to basics
        Mouse:  Find food, collect food, eat food.
        Mouse:  Just like people have done forever
        You:    That sounds pretty nice.
        Mouse:  It is.
<<endif>>

<<if $Learnt_Forage == false>>
        <<if $Learnt_Hiking>>
                Mouse:  Like, when I used to go for those hikes and adventures?
                Mouse:  It wasn't just about walking, and being in nature
                Mouse:  I also used to camp out, 
                Mouse:  Gather water,
                Mouse:  Forage for food,
                Mouse:  To be a part of nature
                You:    Like basic survival mode?
                Mouse:  Yeah I suppose so!
                Mouse:  Back to the basics of life
                You:    That sounds pretty nice
                Mouse:  It is.
        <<endif>>

        <<if $Learnt_Hiking == false>>
                Mouse:  My favourite thing to do in nature was foraging.
                Mouse:  Exploring the wilderness,
                Mouse:  Looking up plants in my little guide book, 
                Mouse:  And bringing home my own supper!
                You:    That's such a cool thing to be able to do!
                Mouse:  I know right!
                You:    It sounds pretty nice
                Mouse:  It is.
        <<endif>>

        <<set $Learnt_Forage to true>>
<<endif>>
//used to walk and forage, now stuck inside, no motivation
//set LEARNT info: forage

<<set $askedWhatHappy to true>>
<<set $ShowAskHappyOption to false>>

<<jump discussion>>
===

title: askToWalk
---

<<if foundBlackberries() == false>>
//------STEP ONE-----------------------
//if havent foraged or planned yet
        <<jump askToWalkONE>>
<<elseif exploredWoods() == false>>
//------STEP TWO----------------------
//once they've foraged
        <<jump askToWalkTWO>>
<<else>>
//-----STEP THREE---------------------
// once they've been to woods
        <<jump askToWalkTHREE>>
<<endif>>

===

title: askToWalkONE
---
//see if players already ahd this talk
<<if !$askedToWalkONE_done>>
        // they dimiss, too tired, need to be reminded, inspired
        //if !LearntForage = need to find a grounding activity outside to remind them what they 
        //used to enjot
        Mouse:  Not right now, sorry
        You:    C'mon, it's a lovely day for it!
        Mouse:  I'm just too tired
        Mouse:  Getting out and walking a bunch doesn't sound so fun right now.
        Mouse:  What's even the point?

        <<if $Learnt_Forage>>
                You:    Maybe we could go find some food!
                You:    Like the foraging you told me about?
                Mouse:  That sounds nice,
                Mouse:  But I don't even know where to look. 
                Mouse:  There's probably not even anything to find around here
                        Hmmm...
                        Maybe I need to scope out the area first?
                You:    What if I go by myself first?
                You:    See if there's anything fun to be found?
                Mouse:  Ooh maybe
                Mouse:  Yeah, come back if you find anything!
        <<else>>
                You:    Well, what did you used to do on your adventures?
                You:    Is there a way to make it more fun?
                Mouse:  Hmmm...
                Mouse:  Well maybe if you find something exciting we could look for?
                Mouse:  I used to like foraging for food
                You:    That sounds fun!
                You:    So if I go on a reconnaissance mission first,
                You:    See if there's any wild food growing,
                You:    And if I find anything, you might join?
                Mouse:  Yeah, maybe!
                Mouse:  Would be good to know what's around here 
                Mouse:  Let me know if you find anything!
                <<set $Learnt_Forage to true>>
        <<endif>>
        <<set $askedToWalkONE_done to true>>
        

<<elseif $askedToWalkONE_done>>
//had this conversation before
        Mouse:  No thanks!
        Mouse:  Go find some food growing nearby and show me,
        Mouse:  That should help inspire me.
        You:    Okey doke!
<<endif>>
//if LearntForage = need to bring foraged goods!
// set TASK: forage
===

title: askToWalkTWO
---

//see if theyve had this convo already
<<if !$askedToWalkTWO_done>>
        //they're reminded how much they enjoyed it!
        // you need to plan the route though, good spots, they dont have capacity rn
        You:    Look what I found!
        -> Show them the blackberries
        Mouse:  Oooh that's so exciting!
        Mouse:  We could make a jam or a crumble or anything!
        Mouse:  Where did you find them?
        -> 'Just down the lane, by the entrance to the woods!'
                You:    I bet there's loads more to be found in the actual woods!
                Mouse:  Ooh probably! 
                Mouse:  And woodlands are so fun to explore
                Mouse:  There are so many hidey places and twisty turns to check out!
                You:    So, wanna go for that walk in the woods?
        -> 'You've gotta come with if you want to find out!'
                Mouse:  Very sneaky!
                Mouse:  I am intrigued to find out where we could find things...
                You:    I have a good area in mind we could explore
                You:    So, wanna go for that walk?

        Mouse:  ...It sounds really tempting!
        Mouse:  But I don't think I'm up for a full-on exploration.
        Mouse:  I don't have the mental or physical bandwidth for it right now
        You:    Oh dang
        You:    Well, how can I make it easier?
        Mouse:  Could you explore it first?
        Mouse:  Find some good spots we can hit?
        Mouse:  If you can learn the area,
        Mouse:  And discover some wild food to forage,
        Mouse:  Then I'd feel more comfortable joining you next time!
        Mouse:  The unknown is a lot scarier for me than it used to be
        You:    Yeah of course!
        You:    I'll go explore,
        You:    Draw up a map of the area,
        You:    Collect any wild food I find on the way,
        You:    Mark those spots on the map,
        You:    And then show you so you can decide if you wanna come with!
        Mouse:  That would be perfect!
        Mouse:  Thank you so much for your help!
        Mouse:  Here's a little book to help
        Mouse:  It'll make it easier to spot the edible plants
        You:    Perfect, thankyou!
        Mouse:  No worries!
        Mouse:  I'll see you again when you've explored a bit for us.

        <<unlockWoods>>
<<elseif $askedToWalkTWO_done>>
//already had this chat
        Mouse:  I'm not ready to walk yet.
        Mouse:  Can you go and explore first?
        Mouse:  Go find some wild food growing in the woods
        Mouse:  Collect it to add it to the map
        Mouse:  And then show it to me!
        Mouse:  If you find a few good spots we can go to
        Mouse:  Then it'll be less scary for me to join you!
        You:    Okey doke, will do!
<<endif>>

//set TASK: expore woods, find places to go on map to show them!
===

title: askToWalkTHREE
---
//Show them the map - they judge based on how many cool places were found
You:    I explored the woods!
-> Show them your map

<<if !walkIsGood()>>
// if plan !good = need to reexplore, find more interesting places
        Mouse:  Hmmm...
                Oof.
                Not a great sign.
        You:    What's up?
        Mouse:  There's just...
        Mouse:  Not many cool places to forage marked on this map.
        You:    Dang

        -> 'Shall I go find some more?'
                Mouse:  I'd really appreciate that!
               
        -> 'Can we go anyway?'
                Mouse:  Hmmm...
                Mouse:  I'm just not really feeling inspired
                Mouse:  I'd like a better idea of where we'd go
                Mouse:  And what we'd see.
        
        You:    Anything in particular you want me to find?
        Mouse:  Just some more edible plants we can collect!
        Mouse:  They should be easy to spot and identify now you have the book
        Mouse:  Find some plants, look them up,
        Mouse:  And gather them to add a pin to the map!
        Mouse:  That way we can go back and find them together!
        You:    I'm sure I can do that!
        Mouse:  Thank you so much!
        Mouse:  If you can find at least 3 types of food growing in the woods that would be great
        Mouse:  See you again soon!
        You:    Seeya in a bit!

<<elseif walkIsGood()>>
//if plan == good = HELPED! Diary entry appears
        Mouse:  Oh this looks GREAT!
        Mouse:  So many cool spots!
        Mouse:  I have a great recipe for this one!
        Mouse:  And have always wanted to try this!
                Sounds pretty promising
        You:    Sooo...
        You:    Wanna head out and collect some of these things together?
        Mouse:  Yes please!
        Mouse:  Let me just put my hikers on!
                WOOHOO!
<<endif>>

===
title: conclusion
---
Mouse:  Thank you so much for encouraging me to get out again!
Mouse:  Your help made it so much easier!
You:    No worries at all!
-> 'It actually made me feel a bit better'
        You:    It was nice to get outside
        You:    And even better to do it with a...
                Say it
        You:    Friend?
        Mouse:  It was nice, wasn't it!
        Mouse:  I'm glad to have made a friend like you 
-> 'How are you feeling now?'
        Mouse:  I think...
        Mouse:  A bit better?
        Mouse:  Not completely
        Mouse:  But I feel a bit more awake, 
        Mouse:  And I'm a bit less numb
        You:    That's so great to hear!
        You:    A step in the right direction
        Mouse:  Thanks for helping me to get there!
        Mouse:  I'm glad to have made a friend like you.
                AWWWW
        You:    Me too. 

Mouse:  Maybe we can make it a more regular thing?
You:    That sounds lovely
Mouse:  Maybe next week?
Mouse:  Now I've taken the first step and gotten out again,
Mouse:  I think I'm feeling confident enough
Mouse:  To plan the next one by myself!
You:    I can't wait to see where you take us
Mouse:  I think it'll be pretty nice
You:    I'm sure it will be
You:    Thank you for planning it
Mouse:  Thank you for helping


===
